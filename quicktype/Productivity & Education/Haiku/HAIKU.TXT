1 REM HAIKU, Vers. 1.2 
2 REM from Experiments in Artificial Intelligence for Small Computers by John Krutch
3 REM ported to BBC BASIC by lurkio @ Stardot, 2021-12-07

5 *FX15
6 MODE 7:PRINT TAB(0,9)"Press a key...";:T=TIME:REPEATUNTILTIME-T>600ORINKEY(0)<>-1:IFRND(-TIME)

10 REM Supervisor 

20 GOSUB 120:REM Initialization 
30 GOTO 50 
40 GOSUB 200:REM Input 
50 GOSUB 260:REM Determine haiku pattern
60 IF RAN = 1 GOSUB 450:REM First haiku pattern
70 IF RAN = 2 GOSUB 670:REM Second haiku pattern
80 IF RAN = 3 GOSUB 890:REM Third haiku pattern
90 IF RAN = 4 GOSUB 1110:REM Fourth haiku pattern 
100 GOTO 40

120 REM Initialization
125 CLS:PRINT TAB(0,8):VDU23,1,0;0;0;0;:LIN$=STRING$(50,"*"):LIN$="":
140 ARTNMB = 4
150 ADJNMB = 50
160 NOUNNMB = 50
170 VERBNMB = 14
180 PREPNMB = 6
190 RETURN 

200 REM Input 
210 LIN$ = "" 
220 RESTORE:*FX15
230 VDU23,1,1;0;0;0;:T=TIME:REPEATUNTILTIME-T>1000ORINKEY(0)<>-1
240 CLS:PRINT TAB(0,8):VDU23,1,0;0;0;0;
250 RETURN 

260 REM Determine haiku pattern 
270 RAN = RND(4)
280 RETURN 

300 REM Check articles 
305 FOR I = 1 TO LEN(LIN$) - 2 
310 IF MID$(LIN$, I, 3) = " A " THEN B$ = MID$(LIN$, I + 3, 1):IF B$ = "A" OR B$ = "E" OR B$ = "I" OR B$ = "O" OR B$ = "U" THEN LIN$ = LEFT$(LIN$, I+1) + "N" + MID$(LIN$, I+2)
320 NEXT I 
330 FOR I = 1 TO LEN(LIN$) - 2 
340 IF MID$(LIN$, I, 4) <> " AN " ELSE B$ = MID$(LIN$, I + 4, 1):IF B$ = "A" OR B$ = "E" OR B$ = "I" OR B$ = "O" OR B$ = "U" THEN 350 ELSE LIN$ = LEFT$(LIN$, I+1) + MID$(LIN$, I+3)
350 NEXT I 
360 RETURN 

380 REM Choose words of poem 
385 RAN = RND(N) + P 
390 FOR I = 1 TO RAN 
400 READ WRD$ 
410 NEXT I 
420 LIN$ = LIN$ + " " + WRD$
430 RESTORE 
440 RETURN 

450 REM First haiku pattern 
460 N = ARTNMB:P = 0:GOSUB 380
470 N = ADJNMB:P = ARTNMB:GOSUB 380
480 N = NOUNNMB:P = ARTNMB + ADJNMB:GOSUB 380 
490 GOSUB 300 
500 LIN$=LIN$ + "..."
510 PRINT MID$(LIN$,2)
520 LIN$=""
530 N = ARTNMB:P = 0:GOSUB 380
540 N = NOUNNMB:P = ARTNMB + ADJNMB:GOSUB 380
550 N = VERBNMB:P = ARTNMB + ADJNMB + NOUNNMB:GOSUB 380
560 N = PREPNMB:P = ARTNMB + ADJNMB + NOUNNMB + VERBNMB:GOSUB 380 
570 N = ARTNMB:P = 0:GOSUB 380
580 N = NOUNNMB:P = ARTNMB + ADJNMB:GOSUB 380 
590 GOSUB 300
600 PRINT MID$(LIN$,2) 
610 LIN$ = "" 
620 N = ADJNMB:P = ARTNMB:GOSUB 380
630 GOSUB 380 
640 N = NOUNNMB:P = ARTNMB + ADJNMB:GOSUB 380
650 PRINT MID$(LIN$,2)
660 RETURN 

670 REM Second haiku pattern 
680 N = NOUNNMB:P = ARTNMB + ADJNMB:GOSUB 380
690 N = PREPNMB:P = ARTNMB + ADJNMB + NOUNNMB + VERBNMB:GOSUB 380
700 N = ARTNMB:P = 0:GOSUB 380
710 N = NOUNNMB:P = ARTNMB + ADJNMB:GOSUB 380
720 GOSUB 300
730 LIN$ = LIN$ + ";" 
740 PRINT MID$(LIN$,2)
750 LIN$ = "" 
760 N = ARTNMB:P = 0:GOSUB 380 
770 N = ADJNMB:P = ARTNMB:GOSUB 380
780 N = NOUNNMB:P = ARTNMB + ADJNMB:GOSUB 380
790 N = PREPNMB:P = ARTNMB + ADJNMB + NOUNNMB + VERBNMB:GOSUB 380 
800 N = ARTNMB:P = 0:GOSUB 380
810 N = NOUNNMB:P = ARTNMB + ADJNMB:GOSUB 380
820 GOSUB 300
830 PRINT MID$(LIN$,2)
840 LIN$ = ""
850 N = ADJNMB:P = ARTNMB:GOSUB 380
860 N = NOUNNMB:P = ARTNMB + ADJNMB:GOSUB 380
870 PRINT MID$(LIN$,2)
880 RETURN 

890 REM Third haiku pattern 
900 N = ARTNMB:P = 0:GOSUB 380
910 N = ADJNMB:P = ARTNMB:GOSUB 380
920 GOSUB 380
930 N = NOUNNMB:P = ARTNMB + ADJNMB:GOSUB 380
940 GOSUB 300
950 LIN$ = LIN$ + ";" 
960 PRINT MID$(LIN$,2)
970 LIN$ = ""
980 N = PREPNMB:P = ARTNMB + ADJNMB + NOUNNMB + VERBNMB:GOSUB 380 
990 N = ARTNMB:P = 0:GOSUB 380
1000 N = ADJNMB:P = ARTNMB:GOSUB 380
1010 N = NOUNNMB:P = ARTNMB + ADJNMB:GOSUB 380
1020 GOSUB 300
1030 PRINT MID$(LIN$,2) 
1040 LIN$ = ""
1050 N = ARTNMB:P = 0:GOSUB 380
1060 N = NOUNNMB:P = ARTNMB + ADJNMB:GOSUB 380
1070 N = VERBNMB:P = ARTNMB + ADJNMB + NOUNNMB:GOSUB 380
1080 GOSUB 300
1090 PRINT MID$(LIN$,2)
1100 RETURN 

1110 REM Fourth haiku pattern 
1120 N = ARTNMB:P = 0:GOSUB 380
1130 N = ADJNMB:P = ARTNMB:GOSUB 380
1140 N = NOUNNMB:P = ARTNMB + ADJNMB:GOSUB 380
1150 N = VERBNMB:P = ARTNMB + ADJNMB + NOUNNMB:GOSUB 380
1160 GOSUB 300
1170 LIN$ = LIN$ + ""
1180 PRINT MID$(LIN$,2)
1190 LIN$=""
1200 N = ARTNMB:P = 0:GOSUB 380
1210 N = ADJNMB:P = ARTNMB:GOSUB 380
1220 GOSUB 380
1230 N = NOUNNMB:P = ARTNMB + ADJNMB:GOSUB 380
1240 GOSUB 300
1250 PRINT MID$(LIN$,2)
1260 LIN$ = "" 
1270 N = PREPNMB:P = ARTNMB + ADJNMB + NOUNNMB + VERBNMB:GOSUB 380 
1280 N = ARTNMB:P = 0:GOSUB 380
1290 N = ADJNMB:P = ARTNMB:GOSUB 380
1300 N = NOUNNMB:P = ARTNMB + ADJNMB:GOSUB 380
1310 GOSUB 300
1320 PRINT MID$(LIN$,2)
1330 RETURN 

2000 REM Program vocabulary
2010 REM A. Articles
2020 DATA A,THE,AN,THE
2030 REM B. Adjectives
2040 DATA AUTUMN,HIDDEN,BITTER,MISTY,SILENT,EMPTY
2050 DATA DRY,DARK,SUMMER,ICY,DELICATE,QUIET
2060 DATA WHITE,COOL,SPRING,WINTER,DAPPLED
2070 DATA TWILIGHT,DAWN,CRIMSON,WISPY,AZURE
2080 DATA BLUE,BILLOWING,BROKEN,COLD,DAMP,FALLING
2090 DATA FROSTY,GREEN,LONG,LATE,LINGERING,LIMPID
2100 DATA LITTLE,MORNING,MUDDY,OLD,RED,ROUGH
2110 DATA STILL,SMALL,SPARKLING,THROBBING,VERMILION
2120 DATA WANDERING,WITHERED,WILD,BLACK,YOUNG
2130 REM C. Nouns 
2140 DATA WATERFALL,RIVER,BREEZE,MOON
2150 DATA RAIN,WIND,SEA,MORNING,SNOW,LAKE,SUNSET
2160 DATA PINE,SHADOW,LEAF,DAWN,GLITTER,FOREST
2170 DATA HILL,CLOUD,MEADOW,SUN,GLADE,BIRD,BROOK
2180 DATA BUTTERFLY,BUSH,DEW,DUST,FIELD,FIR
2190 DATA FLOWER,FIREFLY,FEATHER,GRASS,HAZE,MOUNTAIN
2200 DATA NIGHT,POND,SHADE,SNOWFLAKE
2210 DATA SILENCE,SOUND,SKY,SHAPE,SURF,THUNDER
2220 DATA VIOLET,WATER,WILDFLOWER,WAVE
2230 REM D. Verbs 
2240 DATA SHAKES,DRIFTS,HAS STOPPED,STRUGGLES
2250 DATA HAS FALLEN,HAS PASSED,SLEEPS,CREEPS
2260 DATA FLUTTERS,HAS RISEN,IS FALLING,IS TRICKLING
2270 DATA MURMURS,IS FLOATING
2280 REM E. Prepositions 
2290 DATA ON,IN,OF,UNDER,OVER,NEAR
